window.Backbone ||= {}
window.Backbone.Views ||= {}
window.Backbone.View.prototype.eventAggregator = _.extend({}, Backbone.Events);

class Backbone.Views.EditLinkView extends Backbone.View
  template: JST['backbone/templates/edit_link']

  events:
    'click .save': 'saveLink'

  initialize: (options = {}) ->
    @short_url = options.short_url
    @render()

  saveLink: (event) =>
    event.preventDefault()

    data =
      url: @$el.find('#url').val()
      short_name: @$el.find('#short_name').val()
      private: @$el.find('#private').is(':checked')

    $.ajax(
      url: "/short_urls/#{@short_url.id}"
      type: 'PUT'
      data: {short_url: data})
    .done(@renderSuccess)
    .fail(@renderFailure)

  render: ->
    @$el.html(@template(
      url: @short_url.url
      short_name: @short_url.short_name
      private: @short_url.private
    ))

    return @

  renderFailure: (response) ->
    for field, error of $.parseJSON(response.responseText)
      $("##{field}, [for=#{field}]").addClass('error')

  renderSuccess: (shortUrl) =>
    template = JST['backbone/templates/link_added']

    @short_url = shortUrl

    @$el.html(template(
      url: shortUrl.short_name
    ))

    @eventAggregator.trigger('link_updated', shortUrl)
    if(shortUrl.private)
      $('#short_link_url').html('Private')
    else
      $('#short_link_url').html("<a href='#{shortUrl.url}' target='blank'>#{shortUrl.url}</a>")

    new ZeroClipboard(
      @$el.find(".copy-url"),
      moviePath: "<%= asset_path "ZeroClipboard.swf" %>"
    )

    return @

  close: ->
